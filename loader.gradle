apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "me.shedaniel.unified-publishing"

evaluationDependsOn(':common')

def loomPlatform = project.property('loom.platform')
def projectName
if(loomPlatform == 'neoforge')
    projectName = 'NeoForge'
else if(loomPlatform == 'fabric')
    projectName = 'Fabric'
else if (loomPlatform == 'forge') {
    projectName = 'Forge'

    configurations.all {
        resolutionStrategy.force("net.sf.jopt-simple:jopt-simple:5.0.4")
    }
}

def generatedResources = file("src/generated/resources")

sourceSets.main.resources.srcDirs = [ 'src/main/resources', {
    project(':neoforge').file('src/generated/resources').absolutePath
}
]

loom {
    accessWidenerPath.set(project(":common").loom.accessWidenerPath)
    if(loomPlatform == 'neoforge' || loomPlatform == 'forge'){
        runs {
            if(loomPlatform == 'neoforge'){
                data {
                    data()
                    programArgs "--all", "--mod", "${mod_id}"
                    programArgs "--output", generatedResources.absolutePath
                }
            }
            client {
                programArgs "--username", "Replicate###"
            }
        }

        mods {
            forge {
                sourceSet project(":neoforge").sourceSets.main
            }
        }
        if(loomPlatform == 'forge'){
            forge {
                if(accessWidenerFile.exists()) {
                    convertAccessWideners = true
                    extraAccessWideners.add accessWidenerFile.name
                }

                if(file("src/main/resources/${mod_id}.mixins.json").exists()) {
                    mixinConfig "${mod_id}.mixins.json"
                }

                if(getRootProject().file("common/src/main/resources/${mod_id}-common.mixins.json").exists()) {
                    mixinConfig "${mod_id}-common.mixins.json"
                }
            }
        }
    }
}

def resourceTargets = ['fabric.mod.json', 'neoforge.mods.toml', 'mods.toml']
def intoTargets = ["$rootDir/out/production/resources/", "$rootDir/out/production/${project.name}.main/", "$rootDir/bin/main/"]
def replaceProperties = [mod_id                 : mod_id, mod_name: mod_name, mod_version: mod_version,
                         mod_authors            : mod_authors, mod_credits:mod_credits, mod_description: mod_description,
                         logo_file:logo_file,
                         issue_tracker_url      : issue_tracker_url,
                         mod_license            : mod_license,
                         minecraft_version      : minecraft_version, fabric_loader_version: fabric_loader_version,
                         fabric_api_version     : fabric_api_version, repository_url: repository_url,
                         neo_version_range: neo_version_range, neo_version: neo_version, neo_loader_version_range: neo_loader_version_range,
                         forge_version_range : forge_version_range, forge_version: forge_version, forge_loader_version_range: forge_loader_version_range
                            ]
processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }

    intoTargets.each { target ->
        if (file(target).exists()) {
            copy {
                from(sourceSets.main.resources) {
                    include resourceTargets
                    expand replaceProperties
                }
                into target
            }
        }
    }

}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    create("development${projectName}") {
        extendsFrom common
    }
}

architectury {
    platformSetupLoomIde()

    if(loomPlatform == 'fabric') fabric()
    else if (loomPlatform == 'neoforge') neoForge()
}

dependencies {
    // thanks sodium for adding neoforge support, very cool :>
    common_networking_mc_version = common_networking_mc_version != null ? common_networking_mc_version : minecraft_version
    modImplementation "mysticdrew:common-networking-${loomPlatform}:${common_networking_version}-${common_networking_mc_version}"
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-${loomPlatform}:${jei_version}")

    if(loomPlatform != 'forge'){
        modApi "fuzs.forgeconfigapiport:forgeconfigapiport-${loomPlatform}:${forge_config_api_port_version}"
        include "fuzs.forgeconfigapiport:forgeconfigapiport-${loomPlatform}:${forge_config_api_port_version}"

        modApi "mc.mian:indestructible_blocks:${indestructible_blocks_version}+${minecraft_version}-${loomPlatform}"
        include "mc.mian:indestructible_blocks:${indestructible_blocks_version}+${minecraft_version}-${loomPlatform}"
    }

    if(loomPlatform == 'fabric') {
        modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
        modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${minecraft_version}"

        modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-base:${cardinal_components_api_version}"
        modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-entity:${cardinal_components_api_version}"
        include "org.ladysnake.cardinal-components-api:cardinal-components-base:${cardinal_components_api_version}"
        include "org.ladysnake.cardinal-components-api:cardinal-components-entity:${cardinal_components_api_version}"

        modRuntimeOnly("maven.modrinth:sodium:${sodium_version}-fabric")
        modRuntimeOnly("curse.maven:modernfix-${modern_fix_id}:${modern_fix}")
        modRuntimeOnly("curse.maven:ferritecore-${ferritecore_id}:${ferritecore}")
        modRuntimeOnly("curse.maven:mousetweaks-${mouse_tweaks_id}:${mouse_tweaks}")
//        modRuntimeOnly("curse.maven:configured-${configured_id}:${configured_fabric}")
    } else if(loomPlatform == 'forge') {
        forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

        // have to include mixinextras for Forge lol
        compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
        implementation(include("io.github.llamalad7:mixinextras-forge:${mixin_extras_version}"))
    } else if(loomPlatform == 'neoforge'){
        neoForge "net.neoforged:neoforge:${neo_version}"
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':common', configuration: "transformProduction${projectName}")) { transitive false }
}

unifiedPublishing {
    project {
        displayName = "[${projectName}] ${rootProject.minecraft_version} - ${rootProject.mod_version}"
        version="${rootProject.mod_version}"
        releaseType = "${rootProject.mod_release_type}".toLowerCase()
        changelog = new File("${rootProject.projectDir}/changelog.html").text
        gameVersions = [rootProject.minecraft_version]
        mainPublication tasks.remapJar
        gameLoaders = ["${loomPlatform}"]
        relations {
            includes {
                curseforge="common-network"
                modrinth="common-network"
            }
            includes {
                curseforge="forge-config-api-port-fabric"
                modrinth="forge-config-api-port"
            }
            includes {
                curseforge="indestructible-blocks"
                modrinth="indestructible-blocks"
            }
        }

        var CURSE_API_KEY = project.findProperty("curseforge_token") ?: System.getenv("curseforge_token") ?: ""
        if (CURSE_API_KEY != "") {
            curseforge {
                token = CURSE_API_KEY
                id = "${rootProject.curseforge_id}"
                gameVersions.addAll "Java ${rootProject.java_version}"
            }
        }

        var MODRINTH_TOKEN = project.findProperty("modrinth_token") ?: System.getenv("modrinth_token") ?: ""
        if (MODRINTH_TOKEN != "") {
            modrinth {
                token = MODRINTH_TOKEN
                id = "${rootProject.modrinth_id}"
            }
        }
    }
}

shadowJar {
    exclude 'architectury.common.json'

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    onlyIf { loomPlatform != 'forge'}
    if(loomPlatform == 'fabric')
        injectAccessWidener = true

    inputFile = shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = 'dev'
}

sourcesJar {
    def commonSources = project(':common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

idea.module {
    excludeDirs += [
            file('.gradle'),
            file('build'),
            file('run')
    ]
}

publishing {
    publications {
        if(loomPlatform == 'neoforge')
            mavenNeoForge(MavenPublication) {
                groupId = group
                artifactId = mod_id
                version = version + "-${loomPlatform}"
                from components.java
            }
        else
            mavenFabric(MavenPublication) {
                groupId = group
                artifactId = mod_id
                version = version + "-${loomPlatform}"
                from components.java
            }
    }
    repositories {
        mavenLocal()
    }
}
