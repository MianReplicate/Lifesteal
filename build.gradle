plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6.+" apply false
    id "io.github.pacifistmc.forgix" version "1.2.9"
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1' apply false
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
}

forgix {
    group = mod_base_package // This is the common group of the mod which by default in Architectury Template it's defined as "maven_group" in your gradle.properties. If this property is not defined then by default it'll fetch the group from the maven_group property in your gradle.properties
    mergedJarName = "${mod_id}-${mod_version}+${minecraft_version}.jar" // This is the name of the merged jar. If this property is not defined then by default it'll fetch the "archives_base_name" property with the "mod_version" property in your gradle.properties.
    outputDir = "build/libs/Merged" // This is the output directory of the merged jar from the root project. If this property is not defined then by default it's set to "Merged".

    fabric {
        projectName = "fabric" // This is the name of the fabric project. If this property is not defined then by default it'll set to "fabric" since that's the name the Architectury Template uses.
        jarLocation = "build/libs/lifesteal-${mod_version}.jar" // This is the location of the fabric jar from the fabric project. If this property is not defined then by default it fetches the jar with the shortest name.
    }

    neoforge {
        projectName = "neoforge"
        jarLocation = "build/libs/lifesteal-${mod_version}.jar" // This is the location of the forge jar from the forge project. If this property is not defined then by default it fetches the jar with the shortest name.
    }

}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'architectury-plugin'
    apply plugin: "dev.architectury.loom"
    apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

    archivesBaseName = mod_id
    version = mod_version
    group = mod_base_package

    repositories {
        mavenLocal()
        maven { url 'https://maven.parchmentmc.org' }
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven {
            name = 'Ladysnake Mods'
            url = 'https://maven.ladysnake.org/releases'
        }
        maven {
            url 'https://www.cursemaven.com'
            content { includeGroup 'curse.maven' }
        }

        maven {
            url 'https://api.modrinth.com/maven'
            content { includeGroup 'maven.modrinth' }
        }
        maven {
            // location of the maven that hosts JEI files since January 2023
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven {
            // location of a maven mirror for JEI files, as a fallback
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
        maven { url "https://maven.neoforged.net/releases/" }
        maven { url 'https://jitpack.io' }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of("${java_version}"))
        }

        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(JavaLanguageVersion.of("${java_version}").asInt())

        javaToolchains {
            compilerFor {
                languageVersion.set(JavaLanguageVersion.of("${java_version}"))
            }
        }
    }

    sourceSets.test {
        java.srcDirs = [ ]
        resources.srcDirs = [ ]
    }

    architectury {
        minecraft = "${minecraft_version}"
    }

//    loom {
//        accessWidenerPath.set(project(':common').loom.accessWidenerPath)
//        silentMojangMappingsLicense()
//    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:@YARN_MAPPINGS@:v2"
        mappings(loom.layered {
            officialMojangMappings()

            if(rootProject.hasProperty('parchment_mappings')) {
                def mcVersion = rootProject.hasProperty('parchment_minecraft_version') ? "${parchment_minecraft_version}" : "${minecraft_version}"
                parchment("org.parchmentmc.data:parchment-${mcVersion}:${parchment_mappings}@zip")
            }
        })

        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        compileOnly 'com.google.errorprone:error_prone_annotations:2.11.0'
    }

    processResources {
        project.properties.each {
            inputs.property "${it.key}", "${it.value}"
        }

        filesMatching([ '*.mixins.json', 'pack.mcmeta', 'META-INF/neoforge.mods.toml', 'fabric.mod.json', 'architectury.common.json' ]) {
            expand project.properties
        }
    }


    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}

apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

idea.module {
    excludeDirs += [
            file('.idea'),
            file('.gradle'),
            file('gradle')
    ]
}